#!/bin/tcsh
#
# Configure up a Fedora Core1 Linux machine for the C130 environment
# as a WINDS display, once the basic install is done.
#
# Created on 27 May 2004 Version F1.0.0
# Based on the RDP linconfigF1 from 3 Mar 04.
# Changed to reflect Private Subnetting. 18 Oct 2004
#
# To compensate for this file changing a lot, it's time to start
# keeping track of which version was run and keep that in a file.
#
#
echo ''
set version="WINDS Linux Client Config Version F1.0.0"

# Change the next line do define where we get our RPMs
set RHversion = F1.0.0

# Set Date Stamp for useing as a file postfix
set datestamp = `date +%m%d%g-%H%M`
echo $0 run on $datestamp
echo ''

# define the linux admin machine's ip address here
# Hercules
set confhost = 192.168.84.101

# Append rc.local modifications to this file; the will be put
# in the real rc.local with commented delimiters later
set rclocaladditions =  /tmp/rc.local.additions

#
# Get rid of the #!@$% aliased -i versions of these commands!
#
unalias mv
unalias cp

# Setup some aliases we will use
alias makelink "if (-l \!:2) rm -f \!:2 ; if (! -f \!:2) echo ln -s \!:1 \!:2 ; if (! -f \!:2) ln -s \!:1 \!:2 ; if (! -l \!:2) echo Link \!:1 to \!:2 failed."

alias makedir "if (! -d \!:1) mkdir \!:1"

set HNAME=`uname -n`

echo $version on host $HNAME
echo ''
#echo "-> Make sure host $HNAME has a valid nameserver entry."
echo "-> You may want to run as such: WINDSdisplayconfF1 >& /tmp/wc.out"
echo "-> You have 5 seconds to bail out with control-C"
echo ''
/bin/sleep 5

echo "Ok, here we go...."

ping -w 1 $confhost 
if ($status) then 
	echo Cannot ping $confhost, giving up.
	echo ''
	exit
else set installhost = $confhost
endif

echo $installhost answers pings.
echo ''

echo $version > /etc/WINDSdisplayconfF1

echo "Filling /root/.rhosts"
cp /root/.rhosts /root/.rhosts.$datestamp

cat >/root/.rhosts <<EORHOSTS
hercules
hercules.display.c130
EORHOSTS

echo "Setting permissions on /root/.rhosts"
chown root:sys /root/.rhosts
chmod 600 /root/.rhosts

echo "Setting up /etc/resolv.conf"
cp /etc/resolv.conf /etc/resolv.conf.$datestamp

echo 'domain display.c130' > /etc/resolv.conf
echo 'search display.c130' >> /etc/resolv.conf
echo 'nameserver 192.52.106.6' >> /etc/resolv.conf
echo 'nameserver 192.52.244.5' >> /etc/resolv.conf
echo 'nameserver 192.168.84.101' >> /etc/resolv.conf

#
# THIS MAY BE OBSOLETE
#
echo "Restarting the network to make sure uname is working"
/etc/rc.d/init.d/network restart

#echo "Creating a new hosts file"
#cp /etc/hosts /etc/hosts.$datestamp
#
#set SHNAME=`uname -n | awk -F'.' '{print $1}'`
#set IPADDR=`host $SHNAME | head -1 | awk '{print $4}'`
#echo "127.0.0.1       localhost.localdomain localhost" >/etc/hosts
#echo "$IPADDR   $SHNAME.display.c130 $SHNAME"  >>/etc/hosts
#
#echo "Setting permissions on /etc/hosts"
#chown root:root /etc/hosts
#chmod 644 /etc/hosts

echo "Setting up the ads user and group."
/usr/sbin/groupadd -g 1340 ads
/usr/sbin/useradd -g ads -n -p '$1$iHQWnwzQ$nwcDr1Xls0p2lKGFlsVI5' -u 117 ads

##
## Need a Test here !!!!
##

echo "Setting up Autologin and TCP permissions"
mv /etc/X11/gdm/gdm.conf /etc/X11/gdm/gdm.conf.$datestamp

/bin/sed ' s/AutomaticLoginEnable=false/AutomaticLoginEnable=true/ \
s/AutomaticLogin=/AutomaticLogin=ads/ \
s/#DisallowTCP=true/DisallowTCP=false/' /etc/X11/gdm/gdm.conf.$datestamp > /etc/X11/gdm/gdm.conf

echo "Setting permissions on /etc/X11/gdm/gdm.conf"
chown root:root /etc/X11/gdm/gdm.conf
chmod 644 /etc/X11/gdm/gdm.conf

# mount the /jnet/local directory
echo "Mounting linux directories from $installhost"
makedir /mnt/mnt
#makedir /mnt/mnt2
mount "$installhost":/jnet/local/ /mnt/mnt

set sysconfdir = /mnt/mnt/c130conf
echo "System configuration directory is $sysconfdir"
set adsconfdir = /mnt/mnt/c130conf/adsconf
echo "ads user configuration directory is $adsconfdir"

#/var/log/sudo.log whining
touch /var/log/sudo.log

#/var/log/postgresql whining
touch /var/log/postgresql

#
# Enable rsh only from the 192.168.84. net.  We also add "rsh" to the
# /etc/securetty file so that rsh works for root.
#
echo "Enabling rsh for the .84 net"
mv -f /etc/xinetd.d/rsh /etc/xinetd.d/rsh.$datestamp
cp $sysconfdir/xinet_rshf1 /etc/xinetd.d/rsh
chown root:root /etc/xinetd.d/rsh
chmod 644 /etc/xinetd.d/rsh
echo rsh >> /etc/securetty

#
# Move over a complete hosts file.
#
echo "Replaceing /etc/hosts with complete one."
mv /etc/hosts /etc/hosts.$datestamp
cp -f $sysconfdir/hosts /etc/hosts

echo "Setting permissions on /etc/hosts"
chown root:root /etc/hosts
chmod 644 /etc/hosts

#
# Put in the good ntp.conf, and make sure the driftfile it
# references (if any) exists
#
echo "Replacing /etc/ntp.conf with local version"
mv /etc/ntp.conf /etc/ntp.conf.$datestamp
cp -f $sysconfdir/ntp.conf /etc/ntp.conf
chmod 644 /etc/ntp.conf

set driftfile = `fgrep driftfile /etc/ntp.conf | awk '{ print $2 }'`
if ($%driftfile != 0 && ! -f "$driftfile") then
  echo 0.0 > $driftfile
endif

echo "Setting up tcp wrappers"
cp -f $sysconfdir/hosts.deny /etc
cp -f $sysconfdir/hosts.allow /etc

#
# Coping ads user .rhost file
#
echo "Coping .rhost file for ads user."
cp -f $adsconfdir/ads.rhosts /home/ads/.rhosts
chown ads:ads /home/ads/.rhosts
chmod 600 /home/ads/.rhosts

#
# Coping Desktop Links to Ads User Desktop
#
echo "Adding Desktop Shortcuts for ads User"
cp -pr $adsconfdir/Desktop /home/ads/
chown -R ads:ads /home/ads/Desktop

#
# Coping over WINDS and MCRTD scripts
#
echo "Copying WINDS and MCRTD scripts"
cp -pr $adsconfdir/scripts /home/ads/
chown -R ads:ads /home/ads/scripts

#
# Coping over Xchat Server conf file.
#
echo "Coping Xchat Server config file."
cp -f $adsconfdir/xchat2/servlist_.conf /home/ads/.xchat2

echo "Unmounting linux directories from $installhost"
cd /
umount /mnt/mnt

#
# Disable some cron stuff: newsgroup downloads and tripwire
#
echo "Disabling some cron entries"
set cronfiles=`find /etc/cron.hourly -name "inn*"; find /etc/cron.daily -name "inn*" -o -name "slrnpull*" -o -name "tripwire* "`
foreach file ($cronfiles END)
	if ($file != END && $file !~ "*~") mv -f $file $file~
end

#killing some cron entires
chmod -x /etc/cron.daily/dbbackup
chmod -x /etc/cron.daily/tripwire-check
chmod -x /etc/cron.daily/makewhatis.cron
chmod -x /etc/cron.daily/tetex.cron
chmod -x /etc/log.d/scripts/logwatch.pl

chmod -x /etc/cron.weekly/wwwoffle-purge
chmod -x /etc/cron.weekly/makewhatis.cron

#remove mailman's local crontab
crontab -u mailman -r

echo "Tweaking eject permissions"
if (-f /usr/bin/eject) then
	chmod 755 /usr/bin/eject
	chmod u+s /usr/bin/eject
endif

#
# Append the rc.local mods from above to the real rc.local
#
echo "Modifying /etc/rc.d/rc.local"

echo "\n## linconfig $version additions begin here\n" >> /etc/rc.d/rc.local
cat $rclocaladditions >> /etc/rc.d/rc.local
echo "\n## end of linconfig additions" >> /etc/rc.d/rc.local

rm -f $rclocaladditions

chmod 755 /etc/rc.d/rc.local


cat <<EOLSTUFF


- run /usr/sbin/ntsysv to enable the correct daemons.

- Install *required* security patches

- REBOOT the machine
 
EOLSTUFF
