# -*- python -*-

import os
env = Environment(platform = 'posix',ENV= os.environ)
env['JLOCAL'] = os.environ['JLOCAL']

env.Append(CXXFLAGS='-g -Werror')
env.Append(CPPPATH = '/opt/local/include')
env.Append(LIBPATH = '/opt/local/lib')
env.Append(LIBS = ['pq'])

# To get netcdf libraries on the link command line with static linking,
# they need to be added to the end of the link command.  Adding them to
# LINKFLAGS does not work, because LINKFLAGS is expanded on the command
# line *before* LIBFLAGS.  There is no way to specify the libraries as
# static in LIBS, since members of LIBS are always expanded to -l<lib>.
# The only way to avoid that would be to replace the _stripixes() function,
# and I don't want to go there.  I've been there, and it's not pretty.

env['NETCDFLIBS'] = ['-Wl,-Bstatic', '-lnetcdf_c++', '-lnetcdf',
                     '-Wl,-Bdynamic']
env['LINKCOM'] = env['LINKCOM'] + " $NETCDFLIBS"

# The Configure context is just being used as a check here.  We don't want
# the checks to affect the special linking setup above by appending
# libraries to LIBS.  Thus we create the context on a copy of the
# environment, and we don't use the Finish()ed product.  All the Configure
# checks appeared to do before were add -lnetcdf_c++ and -lpq to the LIBS,
# but those had already been given on the command line anyway.  Really this
# could be removed, since if netcdf or pq aren't installed, then the build
# will still fail.  However it's left here in case someday someone wants to
# really make it work.

conf = Configure(env.Copy())
if not conf.CheckLibWithHeader('netcdf', 'netcdf.h', 'C'):
    print 'netCDF must be installed!'
    Exit(1)

if not conf.CheckLibWithHeader('netcdf_c++', 'netcdf.hh', 'C++'):
    print 'netCDF C++ must be installed!'
    Exit(1)

if not conf.CheckLibWithHeader('pq', 'libpq-fe.h', 'C'):
    print 'PostgreSQL must be installed!'
    Exit(1)

# As mentioned above, we need to ignore the alterations Configure
# makes to the environment.
# env = conf.Finish()

# If you rely on Configure to add libraries, you have to check for them in
# reverse order of their dependencies, ie, check for netcdf before
# netcdf_c++, otherwise netcdf_c++ cannot be linked.  However, when
# statically linking, that means the libraries need to be listed in LIBS in
# the reverse order they are checked by Configure.  Since at the moment
# Configure is not being used to add to LIBS, we don't need this.
# env['LIBS'].reverse()

# print env.Dump()

env.Program('acTrack2kml.cc')

Alias('install', env.Install('$JLOCAL/bin', 'acTrack2kml'))
