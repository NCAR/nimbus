#!/bin/csh

@ num = 0
if ($#argv > 0) then
  @ num = $1
endif

# Environment variables that are needed:
if ( ! $?JLOCAL ) then
    setenv JLOCAL /opt/local
    echo "Environment variable JLOCAL not set, setting to $JLOCAL."
endif
# setenv LOCAL /opt/local
# setenv WINDS $JLOCAL

# For a stand alone installation such as a groundstation or aircraft
# server, JLOCAL will most likely be the same as LOCAL.  In-house
# LOCAL points to the EOL wide installation area and JLOCAL points to
# RAF specific installation area.

if ( ! $?LOCAL ) then
    echo "Need to set environment variables LOCAL."
    exit 1
endif

# Check for netCDF installation.
ls /usr/lib/libnetcdf* >& /dev/null
set rc1 = $status
ls $JLOCAL/lib/libnetcdf* >& /dev/null
set rc2 = $status

if ($rc1 == 1 && $rc2 == 1) then
    echo "netCDF appears to NOT be installed in either /usr/lib or $JLOCAL."
    exit 1
endif

if ( ! $?WINDS ) then
    echo "WINDS not set, ADS2 and winds will NOT be built."
endif

# Pre-req's that most likely exist on your system, Motif, Qt.
# Pre-req's that you will need to install are netcdf & Qwt.

if ($#argv > 0) then
  if ("$argv[1]" == init) then
    # Copy the netcdf dependencies into place
    setenv OPT_LOCAL /net/opt_lnx/local_fc2/.
    echo "Copying netcdf dependencies from $OPT_LOCAL..."
    ( cd $OPT_LOCAL && rsync -avR `find . -name "*netcdf*" -o -name "*ncvalues*"` $JLOCAL )
  endif
endif

# Make sure all the [obvious] directories exist
test -d ${JLOCAL}/bin || mkdir ${JLOCAL}/bin
test -d ${JLOCAL}/lib || mkdir ${JLOCAL}/lib
test -d ${JLOCAL}/include/raf || mkdir -p ${JLOCAL}/include/raf
test -d ${JLOCAL}/man || mkdir ${JLOCAL}/man
test -d ${JLOCAL}/man/man1 || mkdir ${JLOCAL}/man/man1
test -d ${JLOCAL}/man/man3 || mkdir ${JLOCAL}/man/man3

# Build libraries first.  libraf++ needs to go first to install header.h.

foreach file ( libraf libraf++ ads2/hdr_api libpms vardb/lib )
  (cd $file; echo $file; make install)
  if ($num > 0) then
    echo "$file BUILT"
    sleep $num
    reset
  endif
end

# Programs with "install" target.

foreach file ( psql2xml ads2/hdrbld ncplot ncpp/src vardb/editor ads2/xhd xpms2d/src instruments/rdma ads_utils/edADShdr)
  (cd $file; echo ""; make install)
  if ($num > 0) then
    echo "$file BUILT"
    sleep $num
    reset
  endif
end

# Programs using "scons".

foreach file ( nimbus GoogleEarth/acTrack2kml )
  (cd $file; echo $file; scons install)
  if ($num > 0) then
    echo "$file BUILT"
    sleep $num
    reset
  endif
end

# Programs withOUT install target.  WINDS requires xview toolkit.

if ( $?WINDS ) then
  cd ad2
  foreach file ( ads/disc/src ads/dsm/src winds/src )
    (cd $file; echo $file; make)
    if ($num > 0) then
      echo "$file BUILT"
      sleep $num
      reset
    endif
  end
  cd ..
endif


# Programs with Qt [and Qwt].
# rtplot requires $NIMBUS to be set....
setenv NIMBUS `pwd`/nimbus
foreach file ( instruments/hvps/hvpsdiag instruments/usb2d/usb2diag )
  (cd $file; echo $file; qmake; make)
  if ($num > 0) then
    echo "$file BUILT"
    sleep $num
    reset
  endif
end
unsetenv NIMBUS


# Programs with "install" target.

cd nc_utils
foreach file ( asc2cdf flt_time n2asc ncReorder ncav ncextr ncfillvar ncmerge nc_sane )
  (cd $file; echo $file; make install)
  if ($num > 0) then
    echo "$file BUILT"
    sleep $num
    reset
  endif
end
cd ..
