#!/bin/csh

# Environment variables that are needed:
# setenv JLOCAL /code/granger/jlocal
# setenv LOCAL $JLOCAL
# setenv WINDS $JLOCAL

if ( ! $?JLOCAL || ! $?LOCAL ) then
    echo "Need to set LOCAL, JLOCAL, and WINDS."
    exit 1
endif

# Pre-req's that most likely exist on your system, Motif, Qt.
# Pre-req's that you will need to install are netcdf & Qwt.

if ($#argv > 0) then
  if ("$argv[1]" == init) then
    # Copy the netcdf dependencies into place
    setenv OPT_LOCAL /net/opt_lnx/local_fc2/.
    echo "Copying netcdf dependencies from $OPT_LOCAL..."
    ( cd $OPT_LOCAL && rsync -avR `find . -name "*netcdf*" -o -name "*ncvalues*"` $JLOCAL )
  endif
endif

# Make sure all the [obvious] directories exist
test -d ${JLOCAL}/bin || mkdir ${JLOCAL}/bin
test -d ${JLOCAL}/lib || mkdir ${JLOCAL}/lib
test -d ${JLOCAL}/include || mkdir ${JLOCAL}/include
test -d ${JLOCAL}/man || mkdir ${JLOCAL}/man
test -d ${JLOCAL}/man/man1 || mkdir ${JLOCAL}/man/man1
test -d ${JLOCAL}/man/man3 || mkdir ${JLOCAL}/man/man3

# Build libraries first.  libraf++ needs to go first to install header.h.

foreach file ( libraf++ hdr_api libraf libaircraft libpms vardb/lib )
  (cd $file; echo ""; make install)
end

# Programs with "install" target.

foreach file ( hdrbld ncplot ncpp/src vardb/editor xhd xpms2d/src nimbus/src/qc mcr/display/src mcr/processor/src rdma ads_utils/edADShdr)
  (cd $file; echo ""; make install)
end

# Programs using "scons".

foreach file ( nimbus )
  (cd $file; echo ""; scons install)
end

# Programs withOUT install target.  WINDS requires xview toolkit.

#foreach file ( ads/disc/src ads/dsm/src winds/src )
#foreach file ( winds/src )
#  (cd $file; echo ""; make)
#end


# Programs with Qt [and Qwt].

foreach file ( rtplot ads_utils/hvpsdiag )
  (cd $file; echo ""; qmake; make)
end


# Programs with "install" target.

cd nc_utils
foreach file ( asc2cdf n2asc ncReorder ncav ncextr ncmerge )
  (cd $file; echo ""; make install)
end
cd ..
