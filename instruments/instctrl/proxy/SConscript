import sys

tools = Split("""
  default 
  ssl
  protocol
  boost
  boost_program_options
  qt4
  qtt_qtconfig
  ricutil
""")

env = Environment(tools = tools)

qtmodules = ['QtGui']
env.EnableQt4Modules(qtmodules)

uisources = ['ProxyMainWindow.ui']
env.Uic4(uisources)

sources = Split("""
 main.cpp
 ProxyMainWindow.cpp
""")

headers = Split("""
 ProxyMainWindow.h
 ui_ProxyMainWindow.h
""")

# Create the Qt resource file. The source file name will be returned.
resources  = env.Qrc(['proxy.qrc',])

proxy = env.Program('ric_proxy', [sources, resources])
env.Default(proxy)

# Add proxy to the Linux deploy target
if (sys.platform in ['posix','linux', 'linux2']):
    deploy = env.DeployProgram('ric_proxy')
    env.Default(deploy)

env['DOXYFILE_DICT'].update({ "PROJECT_NAME" : "Proxy" })
doxref = env.Apidocs(sources + headers)

