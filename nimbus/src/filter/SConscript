# -*- python -*-
import os
import sharedlibrary

Import('env')

env.Append(LIBPATH=['#/lib'])
env.Append(LIBPATH=['$JLOCAL/lib'])
if os.path.exists('/usr/lib/openmotif'):
  env.Append(LIBPATH=['/usr/lib/openmotif'])

# In order:
#  AMLIB (Atmospheric Mathmatical Library) calculations.
#  raf 'building blocks'.
#  File access stuff.
#  X windows/GUI.
#  General math libraries.
#  ADS3 real-time libraries.  Moved lower to 'if env.realtime'
env['LIBS'] = Split("""
  std user xlate
  hdr_api
  VarDB domx logx log4cpp boost_regex
  raf
  ncaru netcdf hdf5_hl hdf5 pq
  Xm Xt X11
  gsl gslcblas m
""")

sources = Split("""
UDP_Base.cc
Xwin.c
adsIO.c
ads3IO.cc
amlibfn.c
async.c
average.c
calco.c
cb_defs.c
cb_edit.c
cb_main.c
cb_pause.c
circbuff.c
compute.c
config.cc
config_win.c
depend.c
despike.c
dq.c
error.c
filter.c
globals.cc
grnd_feed.cc
hdr_decode.c
hrloop.c
init.c
injectsd.cc
log.c
logfile.c
lrloop.c
memalloc.cc
minmax.cc
netcdf.c
order.c
phase_shift.c
pms1d.c
popen.c
probe.c
psql.cc
brdcast.cc
rd_blanks.c
rd_ddor.c
rd_defs.c
rd_lag.c
rd_meta.c
rd_mod.c
rd_spike.c
rd_sum.c
rd_xmit.c
rec_decode.c
rtloop.c
search.c
setup.c
textfile.c
timeseg.c
rtloop3.cc
Interpolator.cc
sync_reader.cc
""")

md5o = env.StaticObject('md5.cc', CCFLAGS=env['CCFLAGS']+['-Wno-parentheses'])
sources = sources + [md5o]

# This may be unnecessary and even undesirable with the introduction of
# the ld.so.conf file.  However, leave it in for now, especially for the
# case of building nimbus against the buildbot nidas.
if env.get('NIDAS_LD_RUN_PATH'):
  env.Append(_LIBFLAGS=['-Wl,-R', "${NIDAS_LD_RUN_PATH}"])

libn = env.StaticLibrary('nimbus', sources)

def libnimbus(env):
  if os.path.exists('/usr/lib/openmotif'):
    env.Append(LIBPATH=['/usr/lib/openmotif'])
  env.Append(LIBS=libn)
  # In order:
  #  AMLIB (Atmospheric Mathmatical Library) calculations.
  #  raf 'building blocks'.
  #  File access stuff.
  #  X windows/GUI.
  #  General math libraries.
  #  ADS3 real-time libraries.
  env.Require(['libncaru', 'libxlate', 'libstd', 'libuser', 'netcdf'])
  deplibs = Split("""
    hdr_api pms 
    VarDB domx logx log4cpp boost_regex
    raf raf++
    pq
    Xm Xt X11
    gsl gslcblas m bz2 z
  """)
  env.Append(LIBS=deplibs)

Export('libnimbus')

env.Tool(libnimbus)

nimbus = env.Program('nimbus', ['nimbus.c'])
env.Default(nimbus)
Alias('install', env.Install('$JLOCAL/bin', nimbus))

