# -*- python -*-

import os
import sys

# Rather than depend upon finding eol_scons somewhere else in the RAF
# source tree, expect it to be on the standard search path:
# /usr/share/scons/site_scons or ~/.scons/site_scons.
import eol_scons

def nimbusbase(env):
    env.Require(['buildmode', 'nidas', 'netcdf', 'jlocal'])
    env['CC'] = ['g++']
    env.Append(LIBPATH=['$JLOCAL/lib'])
    env.Append(CPPPATH=['#/include', '#/src/filter'])
    env.Append(CCFLAGS=Split("-Wno-write-strings -Wstrict-aliasing"))

env = Environment(platform='posix', tools=['default', 'svninfo', nimbusbase])

env.SvnInfo('include/svnInfo.h', '.')

Export('env')

##
##  Build include files.
##
SConscript('include/SConscript')

##
##  Build Derived Calculations libraries.
##
SConscript('src/ncaru/src/SConscript')

##
##  Build Derived Calculations libraries.
##
SConscript('src/amlib/SConscript')

##
##  Build nimbus proper.
##
SConscript('src/filter/SConscript')

Alias('install', env.subst("${JLOCAL}/bin"))

SConscript('tests/SConscript')

variables = env.GlobalVariables()
variables.Update(env)
Help(variables.GenerateHelpText(env))

ninja = Environment(tools = ['default', 'ninja'])
ninja.NinjaCheck()
